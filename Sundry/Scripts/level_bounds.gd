@tool
@icon("res://Sundry/Icons/level_bounds.svg")
extends Node2D
class_name Bounds

# ==================================================================================================
# VARIABLES
# ==================================================================================================

@export_range(640, 4096, 32, "suffix:px") var width: int = 640: set = _on_width_changed
@export_range(360, 4096, 32, "suffix:px") var height: int = 360: set = _on_height_changed

# ==================================================================================================
# METHODS
# ==================================================================================================

func _ready() -> void:
	z_index = 256
	
	if Engine.is_editor_hint():
		return
	
	var camera: Camera2D = null
	
	while not camera:
		await get_tree().process_frame
		camera = get_viewport().get_camera_2d()
	
	camera.limit_left = int(global_position.x)
	camera.limit_top = int(global_position.y)
	camera.limit_right = int(global_position.x) + width
	camera.limit_bottom = int(global_position.y) + height
	pass

func _draw() -> void:
	if Engine.is_editor_hint():
		var r: Rect2 = Rect2(Vector2.ZERO, Vector2(width, height))
		draw_rect(r, Color(0.0, 0.0, 1.0, 1.0), false, 3)
		pass
	pass

# ==================================================================================================
# OPERATIONS
# ==================================================================================================

func _on_width_changed(new_width) -> void:
	width = new_width
	queue_redraw()
	pass

func _on_height_changed(new_height) -> void:
	height = new_height
	queue_redraw()
	pass
